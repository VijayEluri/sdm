// The contents of this file are subject to the Mozilla Public
// License Version 1.1 (the "License"); you may not use this file
// except in compliance with the License. You may obtain a copy of
// the License at http://www.mozilla.org/MPL/
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//
// Contributor(s): Contributors are attributed in the source code
// where applicable.
//
// The Original Code is "Stamdata".
//
// The Initial Developer of the Original Code is Trifork Public A/S.
//
// Portions created for the Original Code are Copyright 2011,
// LÃ¦gemiddelstyrelsen. All Rights Reserved.
//
// Portions created for the FMKi Project are Copyright 2011,
// National Board of e-Health (NSI). All Rights Reserved.

import org.apache.tools.ant.filters.*

configurations {
	db
	cargo
	tomcat
}

dependencies {
	compile project(':common')
	compile project(':client')
	compile project(':part-api')
	compile libs.jersey_core
	compile libs.jersey_client
	
	testCompile libs.junit
	testCompile libs.hamcrest
	testCompile libs.mockito
	
	db libs.mysql_driver

	cargo('org.codehaus.cargo:cargo-ant:1.1.0') {
		exclude module: 'cargo-core-container-resin'
		exclude module: 'cargo-core-container-geronimo'
		exclude module: 'cargo-core-container-glassfish'
		exclude module: 'cargo-core-container-jboss'
		exclude module: 'cargo-core-container-jetty'
		exclude module: 'cargo-core-container-jo'
		exclude module: 'cargo-core-container-jonas'
		exclude module: 'cargo-core-container-jrun'
		exclude module: 'cargo-core-container-orion'
		exclude module: 'cargo-core-container-weblogic'
	}

	tomcat libs.hibernate_entitymanager
	tomcat(libs.slf4j_api) {
		 force = true
	}
	tomcat(libs.slf4j_log4j) {
		force = true
	}
	tomcat(libs.jcl_over_slf4j) {
		force = true
	}
	tomcat(libs.log4j) {
		force = true
	}
}

ant.taskdef(resource: 'cargo.tasks', classpath: configurations.cargo.asPath)

def runSql(sqlFile, database) {
	driver = 'com.mysql.jdbc.Driver'
	userid = 'root'
	password = ''
	ant.sql(src: sqlFile, print: false, driver: driver,
		url: "jdbc:mysql://localhost:3306/$database", userid: userid, password: password,
		onerror: 'abort', classpath: configurations.db.asPath)
}

task copyConfig << {
    copy {
		from 'tomcat/config'
		into 'build/config'
		filter(ReplaceTokens, tokens: [buildDir: "$buildDir".toString()])
	}
}

task integrationTest(dependsOn: [':importer:war', ':lookup:war', ':integration-test:build']) << {
	initDb.execute()
	loadData.execute()
    copyConfig.execute()
	startServer()
	sleep 4000 // For test data to be read
	runIntegrationTest.execute()
	stopServer()
}

task initDb << {
	runSql 'db/bootstrap.sql', 'mysql'
	//runSql "$rootDir/db/schema.sql", 'sdm_warehouse'
	runSql "$rootDir/db/schema.sql", 'sdm_warehouse_ig'
}
task runServer << {
    copyConfig.execute()
    startServer()
}

def startServer() {
    logsDir = new File("$projectDir/logs")
    logsDir.mkdirs()
	jvmArgs = "-Dsdm.environment=default,integrationtest"
	debug = hasProperty('debug') ? debug : false
    println "Debug: " + debug
	if (debug) {
        println "Debug enabled, listening on port 3997"
		jvmArgs = jvmArgs + " -Xdebug -Xnoagent -Djava.compiler=NONE -Xrunjdwp:transport=dt_socket,address=3997,suspend=n,server=y"
	}
	ant.cargo(id: 'ig-server', containerId: "tomcat7x", output: "$logsDir/output.log", log: "$logsDir/cargo.log", action: "start", wait: "false") {
		zipUrlInstaller(installUrl: 'http://archive.apache.org/dist/tomcat/tomcat-7/v7.0.12/bin/apache-tomcat-7.0.12.zip')
		extraClasspath(path: configurations.tomcat.asPath + ":$buildDir/config")
		configuration {
			property(name: "tomcat-conf-home", value: "$rootDir/subprojects/integration-test/tomcat")
			property(name: "cargo.servlet.port", value: "8081")
			property(name: "cargo.ssl.servlet.port", value: "8444")
			property(name: "cargo.tomcat.ajp.port", value: "9009")
			property(name: "cargo.rmi.port", value: "2009")
			property(name: "cargo.logging", value: "medium")
			property(name: "catalina.logging.level", value: "medium")
			property(name: "cargo.jvmargs", value: jvmArgs)
			configfile(file: "$projectDir/tomcat/server.xml", toFile: "conf/server.xml")
			deployable(type: "war", file: project(':importer').war.archivePath) {
				property(name: "context", value: "importer")
			}
			deployable(type: "war", file: project(':lookup').war.archivePath) {
				property(name: "context", value: "lookup")
			}
		}
	}
}

task loadData << {
	copy {
		from "$rootDir/subprojects/importer/src/test/resources/data/cpr/testEtablering/D100313.L431102"
		into "$buildDir/pack/stamdata/input/cpr/input/"
	}
}

task runIntegrationTest(type: Test) {
	include '**/*IntegrationTest.*'
}
 
test {
	exclude '**/*IntegrationTest.*'
}

def stopServer() {
	ant.cargo(refId: "ig-server", action: "stop")
}